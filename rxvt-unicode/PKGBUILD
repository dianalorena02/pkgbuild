# $Id: PKGBUILD 129923 2011-06-29 23:05:47Z stephane $
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: tobias <tobias@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# bohoomil

pkgname=rxvt-unicode
pkgname=('rxvt-unicode' 'rxvt-unicode-terminfo')
pkgver=9.19
pkgrel=1
arch=('i686' 'x86_64')
url="http://software.schmorp.de/pkg/rxvt-unicode.html"
groups=('custom')
license=('GPL')
makedepends=('libxft' 'perl')
source=("http://dist.schmorp.de/rxvt-unicode/$pkgname-$pkgver.tar.bz2"
        'urxvt.desktop'
        'urxvtc.desktop'
        'font-width-fix.patch')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i ../font-width-fix.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --with-terminfo=/usr/share/terminfo \
    --enable-256-color \
    --enable-combining \
    --enable-font-styles \
    --enable-iso14755 \
    --enable-keepscrolling \
    --enable-perl \
    --enable-pointer-blank \
    --enable-selectionscrolling \
    --enable-unicode3 \
    --enable-utmp \
    --enable-wtmp \
    --enable-lastlog \
    --enable-xft \
    --disable-startup-notification \
    --disable-smart-resize \
    --disable-xim \
    --disable-next-scroll \
    --disable-rxvt-scroll \
    --disable-xterm-scroll \
    --disable-mousewheel \
    --disable-slipwheeling \
    --disable-pixbuf \
    --disable-transparency \
    --disable-frills
  make
}

package_rxvt-unicode() {
  pkgdesc='An unicode enabled rxvt-clone terminal emulator (urxvt)'
  depends=('rxvt-unicode-terminfo' 'libxft' 'perl')

  # install freedesktop menu
  for _f in urxvt urxvtc; do
    install -Dm644 $_f.desktop "$pkgdir/usr/share/applications/$_f.desktop"
  done

  cd $pkgname-$pkgver
  
  # workaround terminfo installation
  export TERMINFO="$srcdir/terminfo"
  install -d "$TERMINFO"
  make DESTDIR="$pkgdir" install
}

package_rxvt-unicode-terminfo() {
  pkgdesc='Terminfo files for urxvt'
  conflict=('rxvt-unicode<=9.18-6')
  install -dm 755 "$pkgdir/usr/share/"
  mv terminfo "$pkgdir/usr/share/"
}



md5sums=('45ad197eb9b5a840c0f65e3a8131921c'
         '62de335035b6f42842d26116655b9720'
         'd140c46051f96bfad8a6a0aec7e5e7b3'
         'df0c3a8b6bb0578d1b91e4081c47881c')
